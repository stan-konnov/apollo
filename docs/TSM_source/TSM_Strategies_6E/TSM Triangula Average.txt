// PJK Triangular Average Strategy
// Copyright 2018, P.J.Kaufman. All rights reserved.
// Puts the greatest weight on the middle value (linearly weighted)

	inputs :  Length(20), usefutures(true);
	vars   :  sum(0), sumw(0), mid(0), w(0), ix(0), triMA(0), futuresinvestment(25000),
				stockinvestment(10000), size(0), ATRper(20);

	sum = 0;
	sumw = 0;
	mid = intportion(length / 2);
	if mod(length,2) = 0 then begin
{ even number of input items }
		w = 0;
		for ix = 1 to length begin
			if ix <= mid then w = w + 1
				else if ix > mid + 1 then w = w - 1;
			sum = sum + w * close[ix - 1];
			sumw = sumw + w;
			end;
		end
	else begin
{ odd number of input items }
		w = 0;
		for ix = 1 to length begin
			if ix <= mid + 1 then w = w + 1 else w = w - 1;
			sum = sum + w * close[ix - 1];
			sumw = sumw + w;
			end;
		end;
	if sumw > 0 then
		TriMA = sum / sumw
	else
		TriMA  = 0;

	if usefutures then 
			size = futuresinvestment/(avgtruerange(ATRper)*bigpointvalue)
		else
			size = stockinvestment/close;

	if marketposition <= 0 and TriMA > TriMA[1] then 
			buy size contracts next bar on open
		else if marketposition >= 0 and TriMA < TriMA[1] then
			sell short size contracts next bar on open;

