// TSM Crossmarket SPY-HighYield
// Copyright 2019, P.J.Kaufman. All rights reserved.

// This is an pseudo-arbitrage that buys SPY when it yields less than the
//		average of HYG and JNK over 5 days.
// ES (futurees) in window1
//	SPY in window2
// HYG in window3
// JNK in window4

	inputs:	corrper1(20), corrper2(252), returnthreshold(0.005), corrthreshold(0.65), mindays(5);
	vars:		JNKcorr1(0), HYGcorr1(0), JNKcorr2(0), HYGcorr2(0), retJNK(0), retHYG(0), retSPY(0),
				avg1R(0), avg2R(0), SPYreturndays(0), HYGreturndays(0), JNKreturndays(0), 
				investment(10000), size(0);

	retSPY = close of data2/close[1] of data2 - 1;
	retHYG = close of data3/close[1] of data3 - 1;
	retJNK = close of data4/close[1] of data4 - 1;

	HYGcorr1 = correlation(retSPY,retHYG,corrper1);	
	JNKcorr1 = correlation(retSPY,retJNK,corrper1);
	HYGcorr2 = correlation(retSPY,retHYG,corrper2);	
	JNKcorr2 = correlation(retSPY,retJNK,corrper2);

	avg1R = (HYGcorr1 + JNKcorr1)/2;
	avg2R = (HYGcorr2 + JNKcorr2)/2;

	SPYreturndays = close of data2/close[mindays] of data2 - 1;
	HYGreturndays = close of data3/close[mindays] of data3 - 1;
	JNKreturndays = close of data4/close[mindays] of data4 - 1;


	size = investment/(avgtruerange(20)*bigpointvalue);

// enter when SPY return is less than the combined returns of HYG and JNK
	if avg1R < corrthreshold and 
			SPYreturndays < HYGreturndays + JNKreturndays - returnthreshold then
				buy size contracts next bar on open;

// exit when the correlations return to normal
	if marketposition <> 0 and avg1R > avg2R then sell ("Xcorr") all contracts next bar on open;

// exit if the SPY return is greater than the combined return of HYG and JNK
	if marketposition <> 0 and SPYreturndays >= HYGreturndays + JNKreturndays then
				sell ("Xreturn") all contracts next bar on open;
