[LegacyColorValue = true]; 

{ TSM Martingales with Trend
  Copyright 2011,2018, P J Kaufman. All rights reserved. }

{ period = length of trend }
  input: period(60), cap(8), investment(1000), longonly(false), printPL(true);
  vars:	size(1), equity(0), atrange(0), trend(0), totalsize(0), nseq(0), adate(" ");
  			
  trend = average(close,period);
  	
{ long sequences }  	
  	if trend > trend[1] then begin
  		if marketposition <= 0 then begin
	  			if longonly = false then buy to cover all contracts this bar on close;
{ start sequence }
  	 			totalsize = investment/close;
	 			buy totalsize contracts this bar on close;
				nseq = 1;
 	 			end
// double long position when close is lower, hold if reached cap
  			else if marketposition = 1 and close < close[1] then begin
  				if cap = 0 or nseq < cap then begin
					totalsize = totalsize + totalsize;
					nseq = nseq + 1;
  					buy totalsize contracts this bar on close;
					end;
  				end
{ when price move profitably, reset to original size }  				
  			else if marketposition = 1 and close > close[1] and longonly = false then begin
  				sell all contracts this bar on close;
  				totalsize = investment/close;
	  			buy totalsize contracts this bar on close;
  			end;
  		end;
  			
{ short sequences }  	
  	if trend < trend[1] then begin
  		if marketposition >= 0 then begin
  				sell all contracts this bar on close;
{ start sequence }
				if longonly = false then begin
   				totalsize = investment/close;
 					sell short totalsize contracts this bar on close;
					nseq = 1;
					end;
  				end
// double short position when close is higher, hold if reached cap
  			else if longonly = false and marketposition = -1 and close > close[1] then begin
  				if cap = 0 or nseq < cap then begin
					totalsize = totalsize + totalsize;
					nseq = nseq + 1;
  					sell short totalsize contracts this bar on close;
					end;
  				end
{ when price move profitably, reset to original size }  				
  			else if longonly = false and marketposition = -1 and close < close[1] then begin
  				buy to cover all contracts this bar on close;
  				totalsize = investment/close;
				sell short totalsize contracts this bar on close;
  			end;
  		end;
    	
 	equity = netprofit + openpositionprofit;
  	
  	If printPL then begin
	  	adate = ELdatetostring(date);
  		If Currentbar = 1 then print(file("c:\TradeStation\Martingaleswithtrend.csv"),
  				 "Date,close,trend,size,netPL,Equity");
  		print(file("c:\TradeStation\Martingaleswithtrend.csv"),adate, ",", close:6:5, ",", 
  						trend:6:5, ",", marketposition*totalsize:8:3, ",", 
  						netprofit + openpositionprofit:8:2, ",", equity:8:2);
		end;