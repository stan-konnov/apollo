[LegacyColorValue = true]; 

{ PJK Trend with Vol Filter
  Copyright 2016 P J Kaufman. All rights reserved. }

{ usesizing	
	userisk
	trendperiod = length of trend
	riskoption	
					0=SPY annualized vol
					1=VIX-I
					2=VXX (tradeable VIX)
	volperiod = length of annualized vol
	volthreshold = entry threshold
	volexitthreshold = exit threshold
	sizeoption	0, single contract or 100 shares
					1 = investment/price
					2 = use price volatility
					3 = use VIX
	}
  
input: 	userisk(false), trendperiod(120), volperiod(20), 
			riskentrythreshold(.40), riskexitthreshold(.50), 
			investment(10000), printoption(false);

  vars:	avol(0), trend(0), signal(0), psignal(0), size(1), entrysize(0), volexit(false),
			returns(0), adate(" "), equity(0), PL(0), trenddir(0);

	psignal = marketposition;

// use price for sizing
	size = investment/close;

// annualized vol
	returns = close/close[1] - 1;
	
	avol = 100*standarddev(returns,volperiod,2)*squareroot(252.);

// trend
	trend = average(close,trendperiod);
	if trend >= trend[1] then trenddir = 1 else trenddir = -1;

// exits first
	if volexit = false and marketposition > 0 then begin
		if trend < trend[1] then begin
				sell ("TRExitLong") all contracts next bar on open;	
				volexit = true;
				end
			else if userisk and avol > riskexitthreshold then begin
				sell ("VolExitLong") all contracts next bar on open;
				volexit = true;
			end;
		end;
	if volexit = false and marketposition < 0 then begin
		if trend > trend[1] then begin
				buy to cover ("TRExitShort") all contracts next bar on open;
				end
			else if userisk and avol > riskexitthreshold then begin
				buy to cover ("VolExitShort") all contracts next bar on open;
			end;
		end;

// reset exit when trend changes
	if trenddir <> trenddir[1] then volexit = false;

// entries
	if volexit = false then begin
		if marketposition <= 0 and trenddir > 0 and 
					(userisk = false or (avol < riskentrythreshold and avol < riskexitthreshold)) then begin
				entrysize = size;
				buy ("NewLong") size contracts next bar on open;
				end
			else if marketposition >= 0 and trenddir < 0 and 
					(userisk = false or (avol < riskentrythreshold and avol < riskexitthreshold)) then begin
				sell short ("NewShort") size contracts next bar on open;
				entrysize = size;
			end;
		end;
  	
 	equity = netprofit + openpositionprofit;
	PL = equity - equity[1];

 	adate = ELdatetostring(date);
	if printoption then begin
		If Currentbar = 1 then begin
//			print(file("c:\tradestation\Trend_with_Vol_Filter.csv"), "Date,size,PLtoday,netPL");
			print(file("c:\tradestation\Trend_with_Vol_Filter_detail.csv"), "Date,Close,Returns,Trend,Dir,AVol,",
						"volexit,Size,Pos,PLtoday,CumPL");
			end;
//		print(file("c:\tradestation\Trend_with_Vol_Filter.csv"),adate, ",", entrysize:8:3, ",", PL:8:4, ",", equity:8:4);
		print(file("c:\tradestation\Trend_with_Vol_Filter_detail.csv"), adate, ",", close:8:4, ",", 
				returns:8:5, ",", trend:8:4, ",", trenddir:3:0, ",", avol:4:5, ",", volexit, ",", 
				currentcontracts:8:3, ",", marketposition:3:0, ",", PL:8:4, ",", equity:8:4);
		end;