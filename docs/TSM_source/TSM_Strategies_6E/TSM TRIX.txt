[LegacyColorValue = true]; 

{ PJK Anticipating TSI: Blau's True Strength Index
		compare with TRIX
   Copyright 2018, PJ Kaufman, All rights reserved.}

// anticipation rules:
//		number of sequence must be rising slower
//		must be above the threshold level (high-low range) over aperiod(250)

	inputs:	TRIXperiod(20), usefutures(true), longonly(false), printfiles(false);

	vars: 	TRIXstep1(0), TRIXstep2(0), TRIXstep3(0), TRIXfinal(0);

	vars:		totalPL(0), PL(0), equity(0), adate(" "), size(0), stockinvestment(10000), 
				Futuresinvestment(25000);

	if usefutures then begin
			size = futuresinvestment/(avgtruerange(20)*bigpointvalue);
			end
		else begin
			size = stockinvestment/close;
		end;

// TRIX logic
// TRIXRaw = XAverage( XAverage( XAverage( Log( close ), Length ) , Length ) , Length ) ;
	TRIXstep1 = XAverage(Log(close),TRIXperiod);
	TRIXstep2 = XAverage(TRIXstep1,TRIXperiod);
	TRIXstep3 = XAverage(TRIXstep2,TRIXperiod);
	TRIXfinal = (TRIXstep3 - TRIXstep3[1])*10000 ;

	if marketposition <= 0 and TRIXfinal > TRIXfinal[1] then begin
			if marketposition < 0 then buy to cover ("TRIXexitS") all shares next bar on open;
			buy ("TRIXbuy") size shares next bar on open;
			end
		else if marketposition >= 0 and TRIXfinal < TRIXfinal[1] then begin
			if marketposition > 0 then sell ("TRIXexitL") all shares next bar on open;
			if longonly = false then sell short ("TRIXsell") size shares next bar on open;
		end;

	totalPL = netprofit + openpositionprofit;
	PL = totalPL - totalPL[1];
	equity = equity + PL;
   
  	If printfiles then begin
	  	adate = ELdatetostring(date);
		if currentbar = 1 then print(file("c:\TradeStation\TRIX.csv"),
			"Date,Close,TRIXstep1,TRIXstep2,TRIXstep2,TRIXfinal,NetPL,Equity,Position");
	
		print(file("c:\TradeStation\TRIX.csv"),adate, ",", close:8:4, ",", TRIXstep1:8:4, ",", TRIXstep2:8:4, ",",
			TRIXstep3:8:4, ",", TRIXfinal:8:4, ",", 
			netprofit+openpositionprofit:8:2, ",", equity:8:2, ",", marketposition*size:5:3);
		end;