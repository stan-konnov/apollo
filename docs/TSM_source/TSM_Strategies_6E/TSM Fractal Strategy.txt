// TSM Fractal Strategy
//	Copyright 2018, P.J.Kaufman. All rights reserved. 
//	Input period must be odd
	
	inputs: 	period(5), takeprofits(false), usestop(false);
	vars:		span(0), MP(0), fractalup(false), fractaldown(false), ix(0), adate(" "),
				size(0), stoploss(0), profittarget(0), fractalhigh(0), fractallow(0),
				signal(0), investment(10000);	

// First calculate if the fractal exists as of the close of the last bar
	span = intportion(period/2);
	MP = span;
	size = investment/close;

// test up fractal
	fractalup = true;
	for ix = period-1 downto 0  begin
		if ix <> MP and high[ix] > high[MP] then fractalup = false;
		end;
	if fractalup then begin
		fractalhigh = highest(high,period);
		fractallow = lowest(low,period);
		end;

// test down fractal
	fractaldown = true;
	for ix = period-1 downto 0 begin
		if ix <> MP and low[ix] < low[MP] then fractaldown = false;
		end;
	if fractaldown then begin
		fractalhigh = highest(high,period);
		fractallow = lowest(low,period);
		end;

// resolve conflicts
	if fractalup and fractaldown then begin
		fractalup = false;
		fractaldown = false;
		stoploss = 0;
		profittarget = 0;
		end;

// stop-loss
	if marketposition <> 0 and usestop then begin
		if marketposition > 0 and low < fractallow then 
					sell ("BuyStop") all shares this bar on close
			else if marketposition < 0 and high > fractalhigh then 
					buy to cover ("SellStop") all shares this bar on close;
		end;

// set profit-taking for new positions
	if marketposition > 0 and takeprofits then begin
			profittarget = entryprice + (fractalhigh - fractallow);
			sell ("LongPT") all shares next bar at profittarget limit;
			end
		else if marketposition < 0 and takeprofits then begin
			profittarget = entryprice - (fractalhigh - fractallow);
			buy to cover ("ShortPT") all shares next bar at profittarget limit;
		end;

// new position
	if marketposition <= 0 and fractaldown then begin
			buy size shares next bar at fractalhigh stop;
			end
		else if marketposition >= 0 and fractalup then begin
			sell short size shares next bar at fractallow stop;
		end;

	signal = marketposition;

	If Currentbar = 1 then 
			print(file("c:\tradestation\Fractal_Strategy.csv"), 
					"Date,High,Low,StartH,StartL,FractUp,FractDn");
		adate = ELdatetostring(date);
		print(file("c:\tradestation\Fractal_Strategy.csv"), adate, ",", 
					High:8:3, ",", Low:8:3, ",", high[period-1]:8:3,  ",", low[period-1]:8:3,
					",", fractalup, ",", fractaldown);