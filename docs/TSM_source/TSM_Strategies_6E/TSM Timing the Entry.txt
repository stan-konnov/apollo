[LegacyColorValue = true]; 

{ TSM Timing entry : Moving average system which uses the RSI for a better entry
  Copyright 2011,2018 P J Kaufman. All rights reserved. }

{ trendperiod = length of calculaton
  maxwait = enter after this many days to avoid missing a trade
  momperiod = length of RSI calculation
  buylevel = momentum trigger point (sell is 100 - buylevel)
  if usefutures = F then stocks are long-only
}
  input: trendperiod(80), maxwait(5), momperiod(8), buylevel(70), usefutures(true), printfile(false);
  vars:	signal(0), trend(0), newtrend(0), totalPL(0), changePL(0), stockinvestment(10000),
  			futuresinvestment(25000), ATRper(20), wait(0), initialentry(0), mom(0), 
			size(0), adate(" ");
  			
	trend = average(close,trendperiod);
	if trend > trend[1] then signal = 1
		else if trend < trend[1] then signal = -1;
	mom = RSI(close,momperiod);

	if usefutures then begin
			size = futuresinvestment/(avgtruerange(ATRper)*bigpointvalue);
			end
		else begin
			size = stockinvestment/close;
		end;

{ new signal }
	if signal <> signal[1] then begin
			if marketposition > 0 then begin
					sell ("trend sell") all contracts this bar on close;
					signal = -1;
					end
				else if marketposition < 0 then begin
					buy to cover ("trend buy") all contracts this bar on close;
					signal = 1;
				end;
			wait = 0;
			initialentry = close;
			end
		else if signal <> 0 and marketposition = 0 then
			wait = wait + 1;
			
{ test for better entry }
	if marketposition = 0 then begin
		if wait >= maxwait then begin
				if	signal > 0 then buy ("maxtime Long") this bar on close
					else if signal < 0 and usefutures then 
						sell short ("maxtime Short") size contracts this bar on close;
				end
// use timing
			else begin
				if signal > 0 and mom < buylevel then buy ("timed Long") this bar on close
					else if signal < 0 and usefutures and mom > 100 - buylevel then 
							sell short ("timed short") size contracts this bar on close;
				end;
		end;
			
  	totalpl = netprofit + openpositionprofit;
  	changepl = totalpl - totalpl[1];
  	
	if printfile then begin
  		If Currentbar = 1 then print(file("c:\TradeStation\TimingtheentryPL.csv"), 
  					"Date,close,trend,mom,signal,initial,wait,pos,totalPL,chgPL");
 		adate = ELdatetostring(date);
 		print(file("c:\TradeStation\TimingtheentryPL.csv"),adate, ",", close:6:4, ",", trend:6:4, ",", 
					mom:6:4, ",", signal:3:0, ",", initialentry:6:4, ",", wait:3:0, ",", 
  					marketposition:3:0, ",", totalpl:5:5, ",", changepl:8:4);
		end;