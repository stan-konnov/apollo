[LegacyColorValue = true]; 

{ TSM Opening Range Breakout
  Copyright 2011,2018 P.J.Kaufman. All rights reserved.  }
  
	inputs: 	openingbars(3), holdovernight(false), precedebyinsideday(false), compressiondays(0),  
				volper(20), minvolatility(500), maxvolatility(1500), usetrend(60), printfile(false);

	vars:		signal(0), topen(0), vol(0), breakoutlevel(0), thigh(0), tlow(0),
				phigh(0), plow(0), pphigh(0), pplow(0), insideday(false), barstoday(0),
				fixhigh(0), fixlow(0), equity(0), PL(0), adate(" "), ix(0), ndays(0),
				trend(0), trenddir(0), compression(false);
	arrays:	trange[5000](0), saveclose[5000](0);

{ new day }
	if date <> date[1] then begin
		ndays = ndays + 1;
		trange[ndays] = thigh - tlow;
		saveclose[ndays] = close[1];
		barstoday = 0;
		pphigh = phigh;
		pplow = plow;
		phigh = thigh;
		plow = tlow;
		if phigh < pphigh and plow > pplow then
			insideday = true else insideday = false;
		signal = 0;
		topen = open;
		thigh = high;
		tlow = low;
{ daily vol  }
		vol = 0;
		if volper < ndays then begin
			for ix = ndays - volper + 1 to ndays begin
				vol = vol + trange[ix];
				end;
			vol = vol / volper;
			vol = vol*bigpointvalue; {dollars}
			end;
{ use trend }
		trenddir = 0;
		if usetrend <> 0 and usetrend < ndays then begin
			for ix = ndays - usetrend + 1 to ndays begin
				trend = trend + saveclose[ix];
				end;
			trend = trend / usetrend;
			if trend > trend[1] then trenddir = 1
				else if trend < trend[1] then trenddir = -1;
			end;

{ compression based on daily data }
		if compressiondays <> 0 and ndays > compressiondays then begin
			compression = true;
			for ix = ndays - compressiondays to ndays - 1 begin
				if trange[ndays] > trange[ix] then compression = false;
				end;
			end;

{ if missed exiting at end of day }
		if holdovernight = false and marketposition <> 0 then begin
				if marketposition > 0 then sell ("XLopen") all contracts this bar on close
					else buy to cover ("XSopen") all contracts this bar on close;
			end;
{ test normal exit first }
		if marketposition <> 0 then begin
 			if vol <> 0 and vol < minvolatility then begin
				if marketposition > 0 then sell ("XLvol") all contracts this bar on close
					else if marketposition < 0 then buy to cover ("XSvol") all contracts this bar on close;
				end;
			if marketposition > 0 and usetrend > 0 and trenddir < 0 then 
					sell ("XLtrend") all contracts this bar on close
				else if marketposition < 0 and usetrend > 0 and trenddir > 0 then
					buy to cover ("XStrend") all contracts this bar on close;
			end;
		end;
		
{ save high and low for today }
	thigh  = maxlist(thigh,high);
	tlow = minlist(tlow,low);

	barstoday = barstoday + 1;
	if barstoday = openingbars then begin
		fixhigh = thigh;
		fixlow = tlow;
		end;

{ all new positions }
	if volper < ndays and (precedebyinsideday = false or insideday) and (compressiondays = 0 or compression) and
			(volper = 0 or vol > minvolatility) and (maxvolatility = 0 or vol < maxvolatility) then begin
{ if short or no position }
		if marketposition <= 0 and (usetrend = 0 or trenddir > 0)then 
				buy ("BOup") 1 contract next bar at fixhigh + 0.00001 stop
			else if marketposition >= 0 and (usetrend = 0 or trenddir < 0) then 
				sell short ("BOdown") 1 contract next bar at fixlow - 0.00001 stop;
		end;
		
	if holdovernight = false and marketposition <> 0 and time >= sess1endtime then begin
			if marketposition > 0 then sell ("XLEOD") all contracts this bar on close
				else buy to cover ("XSEOD") all contracts this bar on close;
		end;
	
	if printfile then begin
	  	equity = netprofit + openpositionprofit;
		PL = equity - equity[1];
		adate = ELdatetostring(date);
		if Currentbar = 1 then begin
   	 	print(file("c:\tradestation\Intraday_N-Bar_Breakout_detail.csv"),"date,time,open,high,low,close,",
						"thigh,tlow,maxbars,nbar,relpos,mbar,mrelpos,pos,openPL,totPL,comment"); 
    		print(file("c:\tradestation\intraday\Intraday_N-Bar_Breakout_PL.csv"),"Date,TotalPL");
			end;

  	 	print(file("c:\tradestation\Intraday_N-Bar_Breakout_detail.csv"), adate, "," ,time:4:0, ",", 
			open:7:4, ",", high:7:4, ",", low:7:4, ",", close:7:4, ",", thigh:7:4, ",", tlow:7:4, ",", 
			fixhigh:7:4, ",", fixlow:7:4, ",", marketposition:3:0, ",", openpositionprofit:8:3, ",", equity:8:3);
  		if date <> date[1] then print(file("c:\tradestation\Intraday_N-Bar_Breakout_PL.csv"), adate, "," ,equity:8:3);
		end;