[LegacyColorValue = true]; 

{ TSM MA Vol Filtered for Futures
  Copyright 1994-1999,2011, 2018 P J Kaufman. All rights reserved. }
// This program is for futures or equities because it uses ATR
{  period = length of calculaton
	volperiod = length of volatility calculation
   highvolfilter	 <> 0 then don't enter if volatiltiy > highfilter*stdev of returns
   lowvolfilter     <> 0 don't enter if volatility < lowfilter*stdev of returns
	reset if reenters valid volatility zone
}
  input: period(40), volperiod(20), useAV(true), useATR(false), lag(60), highfilter(0), 
				lowfilter(0),longonly(true), variablesize(true), printfile(true);
   vars:	signal(0), size(100), investment(10000), trend(0), adate(" "), totalPL(0), 
  			returns(0), vol(0), ATR(0),PL(0), PLlong(0), PLshort(0), wait(0), firsttrade(true),
			trendsignal(0), trendsize(0), trendPL(0), totaltrendPL(0), trenddir(0); 
 

	ATR = avgtruerange(volperiod);
	if variablesize then size = investment/(ATR*bigpointvalue);
	returns = close/close[1] - 1;

	vol = ATR;
	if useATR and ATR[lag] > 0 then begin
//			vol = truerange/ATR[lag];
			end
		else if useAV then begin
			vol = stddev(returns,volperiod)*squareroot(252);
		end;
	trend = average(close,period);

// test "wait" reset when trend changes?
//	if (wait > 0 and trend < trend[1]) or (wait < 0 and trend > trend[1]) then wait = 0;
	if trend > trend[1] then trenddir = 1
		else if trend < trend[1] then trenddir = -1;

	if wait <> 0 and vol < lowfilter then wait = 0;

// exit on trend
	if marketposition > 0 and trenddir < 0 then
			sell all contracts next bar on open
		else if marketposition < 0 and trenddir > 0 then
			buy to cover all contracts next bar on open;

// exit long position on high volatility
	if marketposition > 0 and (highfilter = 0 or vol > highfilter) then begin
			sell ("XLvol") all shares next bar on open;
			wait = marketposition;
			end
// exit short position on high volatility
		else if marketposition < 0 and (highfilter = 0 or vol > highfilter) then begin
			buy to cover ("XSvol") all shares next bar on open;
			wait = marketposition;
		end;

// new entry long
  	if marketposition <= 0 and trenddir > 0 and wait = 0 then begin
  			if marketposition < 0 then buy to cover ("XStrend") all shares next bar on open;
			if (highfilter = 0 or vol <= highfilter) and (lowfilter = 0 or vol >= lowfilter) then begin
  				buy ("Ltrend") size shares next bar on open;
				end;
  			end
// new entry short
	  	else if marketposition >= 0 and trenddir < 0 and wait = 0 then begin
			if marketposition > 0 then sell ("XLtrend") all shares next bar on open;
			if longonly = false and (highfilter = 0 or vol <= highfilter) and 
						(lowfilter = 0 or vol >= lowfilter)then begin
				sell short ("Strend") size shares next bar on open;
				end;
		end;
  			
 	totalPL = netprofit + openpositionprofit;
	PL = totalPL - totalPL[1];
	if marketposition > 0 then
			PLlong = PLlong + PL
		else if marketposition < 0 then
			PLshort = PLshort + PL;

// new trend long 
	if trend > trend[1] and trend[1] < trend[2] then begin
			trendsignal = 1;
			trendsize = investment/(ATR*bigpointvalue);
			end
		else if trend < trend[1] and trend[1] > trend[2] then begin
			trendsignal = -1;
			trendsize = investment/(ATR*bigpointvalue);
		end;
	trendPL = trendsignal[1]*trendsize[1]*(close - close[1])*bigpointvalue;
	totaltrendPL = totaltrendPL + trendPL;

	if printfile then begin
		If Currentbar = 1 then 
			print(file("c:\tradestation\TSM_MA_vol_filtered_for_futures.csv"), 
					"Date,Open,High,Low,Close,Trend,Dir,ATR,Vol,Wait,Size,Vol,PLtoday,PLlong,",
					"PLshort,TotalPL,TrendSig,Trendsize,TrendPL,TotTRPL");
		adate = ELdatetostring(date);
		print(file("c:\tradestation\TSM_MA_vol_filtered_for_futures.csv"), adate, ",", open:6:5, ",",
					high:6:5, ",", low:6:5, ",", close:6:5, ",", trend:6:3, ",", trenddir:5:0, ",", 
					ATR:6:3, ",", vol:6:3, ",", wait:5:0, ",",
					currentcontracts:8:2, ",", vol:4:3, ",", PL:8:2, ",", PLlong:8:2, ",", 
					PLshort:8:2, ",", TotalPL:8:2, ",", trendsignal:5:0, ",", trendsize:5:2, ",",
					 trendPL:8:2, ",", totaltrendPL:10:2);
		end;