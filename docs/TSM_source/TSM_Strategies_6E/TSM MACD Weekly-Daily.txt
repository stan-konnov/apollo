[LegacyColorValue = true]; 

{ PJK MACD Weekly-Daily
   Copyright 2018, PJ Kaufman, All rights reserved.
	Based on Vitali Apirine, "Weekly & Daily MACD," TAS&C Dec 2017 }

// anticipation rules:
//		number of sequence must be rising slower
//		must be above the threshold level (high-low range) over aperiod(250)

	inputs:	FastPer(12), SlowPer(26), 
				usefutures(true), longonly(false), printfiles(false);

	vars: 	FastWeeklyPer(FastPer*5), SlowWeeklyPer(SlowPer*5), FastEMA(0), SlowEMA(0),
				FastWeeklyEMA(0), SlowWeeklyEMA(0), DailyMACD(0), WeeklyMACD(0);

	vars:		totalPL(0), PL(0), equity(0), adate(" "), size(0), stockinvestment(10000), 
				Futuresinvestment(25000);

	if usefutures then begin
			size = futuresinvestment/(avgtruerange(20)*bigpointvalue);
			end
		else begin
			size = stockinvestment/close;
		end;

// Daily MACD
	FastEMA = xaverage(close,FastPer);
	SlowEMA = xaverage(close,SlowPer);
	FastWeeklyEMA = xaverage(close,FastWeeklyPer);
	SlowWeeklyEMA = xaverage(close,SlowWeeklyPer);
	DailyMACD = FastEMA - SlowEMA;
	WeeklyMACD = FastWeeklyEMA - SlowWeeklyEMA;

	if marketposition <= 0 and DailyMACD crosses above WeeklyMACD then begin
			if marketposition < 0 then buy to cover ("ShortExit") all shares next bar on open;
			buy ("LongEntry") size shares next bar on open;
			end
		else if marketposition >= 0 and DailyMACD crosses below WeeklyMACD then begin
			if marketposition > 0 then sell ("LongExit") all shares next bar on open;
			if longonly = false then sell short ("ShortSale") size shares next bar on open;
		end;

	totalPL = netprofit + openpositionprofit;
	PL = totalPL - totalPL[1];
	equity = equity + PL;
   
  	If printfiles then begin
	  	adate = ELdatetostring(date);
		if currentbar = 1 then print(file("c:\TradeStation\MACD_D&W.csv"),
			"Date,Close,FastEMA,SlowEMA,FastWEMA,SlowWEMA,DailyMACD,WeeklyMACD,NetPL,Equity,Position");
	
		print(file("c:\TradeStation\MACD_D&W.csv"),adate, ",", close:8:4, ",", FastEMA:8:4, ",", SlowEMA:8:4, ",",
			FastWeeklyEMA:8:4, ",", SlowWeeklyEMA:8:4, ",", DailyMACD:8:4, ",", WeeklyMACD:8:4, ",", 
			netprofit+openpositionprofit:8:2, ",", equity:8:2, ",", marketposition*size:5:3);
		end;