{ PJK Intraday Timing the Entry
  Copyright 2019 P J Kaufman. All rights reserved. 
	Equities only }

{ 	Trendtype = 0, MA
				 = 1, BO
	period = length of calculaton (bars)
	pullbackoption	= 0, no pullback
						= 1, minimum move
						= 2, better price before N bars
						= 3, percent of n-day ATR
	optionvalue = added spec associated with option
 }
  input: trendtype(0), trendperiod(80), pullbackoption(0), optionvalue(0),
			investment(10000), printPL(true);
  vars:	signal(0), price(0), size(1), trend(0), MA(0), ATRper(20), ATR(0), equity(0),
  			atrange(0), adate(" "), PL(0), longonly(false), orderprice(0), signalprice(0), signalbar(0);
  			
 	size = investment/close;

// set trend
	if trendtype = 0 then begin
			MA = average(close,trendperiod);
			if MA > MA[1] then begin
					trend = 1;
					end
				else if MA < MA[1] then begin
					trend = -1;
				end;
			end
		else if trendtype = 1 then begin
			if close > highest(close,trendperiod)[1] then begin
					trend = 1;
					end
				else if close < lowest(close,trendperiod)[1] then begin
					trend = -1;
				end;
			end;
	if trend[1] <> trend then signalprice = close;

	signal = marketposition;
	if marketposition = 0 then begin
		orderprice = 0;
		end;
	
// normal exit
// always exit immeditately then wait for entry timing	
	if trend > 0 and marketposition < 0 then begin
			buy to cover all contracts next bar on open;
			signal = 0;
			signalprice = close;
			signalbar = -1;
			end
		else if trend < 0 and marketposition > 0 then begin
			sell all contracts next bar on open;
			signal = 0;
			signalprice = close;
			signalbar = -1;
		end;

	signalbar = signalbar + 1;

// benchmark case, always enter
  	If pullbackoption = 0 then begin
  		if trend = 1 and marketposition <> 1 then begin
				Buy size contracts next bar on open;
				signal = 1;
				end
  			else if trend = -1 and marketposition <> -1 then begin
				If longonly = false then begin
					Sell Short size contracts next bar on open;
					signal = -1;
					end;
  			end;
		end;

// test if not yet entered a new position
  		if signal = 0 then begin
// minimum pullback
			if pullbackoption = 1 then begin
 	 				If trend > 0 then begin
							buy ("Bopt1") size shares next bar at signalprice - optionvalue limit;
							end
						else if longonly = false and trend < 0 then begin
							sell short ("SellMin") size shares next bar at signalprice + optionvalue limit;
						end;
					end
// any better closing price before N bars elapse
  				else if pullbackoption = 2 and signalbar < optionvalue then begin
					if trend > 0 and close < signalprice then begin
							buy ("BuyNday") size contracts this bar on close;
							signal = 1;
							end
						else if longonly = false and trend < 0 and close > signalprice then begin
							sell short ("SellNday") size contracts this bar on close;
							signal = -1;
						end;
					end
// entry option 2 if bar time elapsed
				else if pullbackoption = 2 and signalbar >= optionvalue then begin
					if trend > 0 then buy ("BOpt2max") size shares this bar on close
						else if trend < 0 then sell short ("SOpt2max") size shares this bar on close;
					end
// percentage of n-day ATR
				else if pullbackoption = 3 then begin
					ATR = avgtruerange(ATRper);
					if trend > 0 then begin 
							orderprice = signalprice - optionvalue*ATR;
							buy ("BuyATR") size shares next bar at orderprice limit;
							end
						else if longonly = false and trend < 0 then begin
							orderprice = signalprice + optionvalue*ATR;
							sell short ("SellATR") size shares next bar at orderprice limit;
						end;
				end;
		end;
  	
  	If printPL then begin
  		equity = netprofit + openpositionprofit;
		PL = equity - equity[1];
	  	adate = ELdatetostring(date);
		If Currentbar = 1 then print(file("c:\tradestation\EntryTimingTrend.csv"), 
				"Date,Open,High,Low,Close,MA,Trend,ATR,Orderprice,Size,Pos,netPL,PLtoday");
  		print(file("c:\tradestation\EntryTimingTrend.csv"),adate, ",", open:6:5, ",", 
				high:6:5, ",", low:6:5, ",", close:6:5, ",", MA:6:5, ",", trend:3:0, ",",
				ATR:6:4, ",", orderprice:6:5, ",", size:8:3, ",", marketposition:3:0, ",", 
				equity:8:4, ",", PL:8:4);
		end;