{ TSM VIX Mean Reverting Strategy
	Based on MarketSci Blog 3/1/2011
	Copyright 2011,2018 P.J.Kaufman. All rights reserved. }
	
{	TradeOption	1 = long only
					2 = short only
					3 = both long and short }	

	inputs:	period(10), tradeoption(3), useLRS(false), maxbuyvol(0.0), minsellvol(0.0), 
				stoploss(0.10),takeprofits(false), PTperiod(20), PTfactor(1.0), ATRper(20),
				nolongs(false), noshorts(false), tradeonclose(true), trendfilter(0),
				usefutures(true), stockinvestment(10000), futuresinvestment(25000), printfile(false);
	vars:		EMA(0), SMA(0), sc(0), PL(0), longPL(0), shortPL(0), ATR(0), totalPL(0),
				size(0), investment(10000), adate(" "), signal(0), wait(0), PTATR(0), 
				LRS(0), returns(0), vol(0), volOK(true), trend(0), PTtarget(0);

	if currentbar = 1 then begin
		EMA = close;
		SMA = close;
		sc = 2/(period + 1);
		end;

	if usefutures then 
			ATR = Avgtruerange(ATRper);
			if ATR <> 0 then size = futuresinvestment/(ATR*bigpointvalue)
		else 
			if close <> 0 then size = stockinvestment/close;

	if trendfilter <> 0 then trend = average(close,trendfilter);
	
	SMA = average(close,period);
	EMA = EMA + sc*(close - EMA);
	LRS = linearregslope(close,period);
// volatility
	returns = 0;
	if close[1] <> 0 then returns = close/close[1] - 1;
	vol = stddev(returns,PTperiod)*squareroot(252);
	volOK = true;
	if (maxbuyvol <> 0 and vol > maxbuyvol) or 
		(minsellvol <> 0 and vol < minsellvol) then volOK = false;

//	ATR = avgtruerange(period);
	if PTperiod <> 0 then PTATR = avgtruerange(PTperiod);

	if marketposition = 0 then wait = signal;

	if useLRS = false and volOK then begin
// new short when EMA crosses above SMA
		if tradeoption <> 1 and EMA > SMA and marketposition >= 0 and 
				(minsellvol = 0 or vol > minsellvol) and wait >= 0 and 
					(trendfilter = 0 or trend < trend[1]) then begin
					if tradeonclose then sell all contracts this bar on close
						else sell all contracts next bar on open;
					if noshorts = false then begin
						if tradeonclose then sell short size shares this bar on close
							else sell short size shares next bar on open;
// profit-taking
						if takeprofits and PTfactor <> 0 then
							buy to cover ("ShortPT") all shares next bar at close - PTfactor*PTATR limit;
						end;
					wait = 0;
					end
// new long when EMA is above SMA
			else if tradeoption <> 2 and EMA < SMA and marketposition <= 0 and 
					(maxbuyvol = 0 or vol < maxbuyvol) and wait <= 0 and
						(trendfilter = 0 or trend > trend[1]) then begin
				if tradeonclose then buy to cover all shares this bar on close
					else buy to cover all shares next bar on open;
				if nolongs = false then begin
					if tradeonclose then buy size shares this bar on close
						else buy size shares next bar on open;
// profit-taking
					if takeprofits and PTfactor <> 0 then
						sell ("LongPT") all shares next bar at close + PTfactor*PTATR limit;
					end;
				wait = 0;
			end;
		end;

// LRS OPTION ==================================================================
	if useLRS then begin
// new short when EMA crosses above SMA
		if tradeoption <> 1 and close[1] + LRS > SMA and marketposition >= 0 and 
				(minsellvol = 0 or close > minsellvol) and wait >= 0 then begin
					sell short size shares this bar on close;
// profit-taking
					if takeprofits and PTfactor <> 0 then
						buy to cover ("ShortPT2") all shares next bar 
								at close - PTfactor*PTATR limit;
					wait = 0;
					end
// new long when EMA is above SMA
			else if tradeoption <> 2 and close[1] + LRS < SMA and marketposition <= 0 and 
					(maxbuyvol = 0 or close < maxbuyvol) and wait <= 0 then begin
				buy size shares this bar on close;
// profit-taking
				if takeprofits and PTfactor <> 0 then
					sell ("LongPT2") all shares next bar at close + PTfactor*PTATR limit;
				wait = 0;
			end;
		end;
// END OF LRS =====================================================================

	if takeprofits and PTfactor <> 0 then begin
		if marketposition > 0 then begin 
				PTtarget = entryprice + PTfactor*PTATR;
				sell ("LongPT3") all shares next bar at entryprice + PTfactor*PTATR limit;
				end
			else if marketposition < 0 then begin
				PTtarget = entryprice - PTfactor*PTATR;
				buy to cover ("ShortPT3") all shares next bar at entryprice - PTfactor*PTATR limit;
			end;
		end;

// stop-loss
	if stoploss <> 0 then begin
		if marketposition > 0 then 
				sell all shares next bar at entryprice*(1-stoploss) stop
			else if marketposition < 0 then 
				buy to cover all shares next bar at entryprice*(1+stoploss) stop;
		end;
	
// exit EMA + SMA trades ============================================================
	if useLRS = false then begin
		if tradeoption = 1 and EMA > SMA then 
				sell this bar on close
			else if tradeoption = 2 and EMA < SMA then 
				buy to cover this bar on close;
		end;
// exit LRS + SMA trades ============================================================
	if useLRS then begin
		if tradeoption = 1 and close[1] + LRS > SMA then 
				sell this bar on close
			else if tradeoption = 2 and close[1] + LRS < SMA then 
				buy to cover this bar on close;
		end;
// end LRS option ===================================================================
	
 	totalPL = netprofit + openpositionprofit;
	PL = totalPL - totalPL[1];
	if marketposition > 0 or signal > 0 then
			longPL = longPL + PL
		else if marketposition < 0 or signal < 0 then
			shortPL = shortPL + PL;

	signal = marketposition;
  	
	if printfile then begin
		adate = ELdatetostring(date);
	  If Currentbar = 1 then begin
				print(file("c:\TradeStation\VIX_mean_reversion_PL.csv"),
  						"Date,position,today,netPL,longPL,shortPL");
				print(file("c:\TradeStation\VIX_mean_reversion_detail.csv"),
  						"Date,Open,High,Low,Close,MA,EXP,returns,vlty,position,size,PTtarget,",
						"today,longPL,shortPL,netPL");
				end;
  		print(file("c:\TradeStation\VIX_mean_reversion_PL.csv"),adate, ",",
  				 currentcontracts*marketposition:5:0, ",", PL:8:0,  ",",  
  				 longPL:8:2, ",", shortPL:8:2, ",", totalPL:8:2);
  		print(file("c:\TradeStation\VIX_mean_reversion_detail.csv"),adate, ",", open:6:4, ",",
				high:6:4, ",", low:6:4, ",", close:6:4, ",", SMA:6:4, ",", EMA:6:4, ",",
				returns:6:4, ",", vol:6:4, ",", marketposition:3:0, ",", currentcontracts:5:1, ",",
  				 currentcontracts*marketposition:5:0, ",", PTtarget:6:4, ",",  
  				 PL:8:02, ",", longPL:8:2, ",", shortPL:8:2, ",", totalPL:8:2);
		end;
