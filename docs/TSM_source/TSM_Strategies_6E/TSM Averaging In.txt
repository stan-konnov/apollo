[LegacyColorValue = true]; 

{ TSM Averaging In : Moving average system with averaging in
  Copyright 1994-1999,2011,2018,2019, P J Kaufman. All rights reserved. }

{ period = length of trend
  nentries = number of entries
  daysbetweenentries = days between entry points
}
  input: period(80), numberoforders(5), daysbetweenentries(1), usefutures(false), printfile(false);
  vars:	signal(0), contractsheld(0), trend(0), equity(0), rounding(0.95),
  			lastentrybar(0), prevcontracts(0), adate(" "), PLtoday(0),
			size(0), maxsize(0), futuresinvestment(100000), stockinvestment(100000);
  			
	trend = average(close,period);
	if trend > trend[1] then signal = 1
		else if trend < trend[1] then signal = -1;
		
	if signal <> signal[1] then begin
// calculate position size for new trade
		if usefutures then begin
				maxsize = futuresinvestment/(avgtruerange(20)*bigpointvalue);
				size = maxsize/numberoforders;
				end
			else begin
				maxsize = stockinvestment/close;
				size = maxsize/numberoforders;
			end;
// new long position
		if signal > 0 then begin
				if marketposition < 0 then buy to cover all contracts this bar on close;
// if no averaging then enter max position size
				if daysbetweenentries = 0 or numberoforders = 1 
						then buy ("BuyALL") maxsize contracts next bar on open
					else if currentcontracts < maxsize then 
						buy ("new_long") size contracts this bar on close;
				lastentrybar = currentbar;
				end
			else if signal < 0 then begin
				if marketposition > 0 then sell all contracts this bar on close;
				if daysbetweenentries = 0 or numberoforders = 1
						then sell short ("SellAll") maxsize contracts next bar on open
					else 
						sell short ("new_short") size contracts this bar on close;
				lastentrybar = currentbar;
			end;
		end;

// not the first entry
	if marketposition <> 0 and daysbetweenentries > 0 and currentcontracts < maxsize*rounding and 
			currentbar >= lastentrybar + daysbetweenentries then begin
		if marketposition > 0 then begin
				buy ("next_long") size contracts next bar on open;
				lastentrybar = currentbar;
				end
			else if marketposition < 0 then begin
				sell short ("next_short") size contracts next bar on open;
				lastentrybar = currentbar;
			end;
		end;
 	
  	equity = netprofit + openpositionprofit;
	PLtoday = equity - equity[1]; 
  	prevcontracts = currentcontracts;
  	
	if printfile then begin
  		If Currentbar = 1 then print(file("c:\TradeStation\TSM_AveragingIn_PL.csv"), 
  				"Date,close,trend,signal,maxsize,pos,size,lentbarL,DailyPL,TotalP");
		adate = ELdatetostring(date);
  		print(file("c:\TradeStation\TSM_AveragingIn_PL.csv"),adate, ",", close:8:4, ",", 
				trend:6:4, ",", signal:5:0, ",", maxsize:8:2, ",", marketposition:3:0, ",", 
  				currentcontracts:8:3, ",", lastentrybar:8:0, ",", PLtoday:8:4, ",", equity:8:4);
		end;