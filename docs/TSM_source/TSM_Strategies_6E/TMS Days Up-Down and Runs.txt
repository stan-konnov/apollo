// Days Up-Down and Runs
// Copyright 2017, P.J.Kaufman. All rights reserved.
// This program totals the nunmber of up days, down days, average up, average down,
//		and the distribution of up and down runs

vars:	updays(0), downdays(0), unchdays(0), avgup(0), avgdown(0), currentrun(0), 
			up(0), down(0), kx(0);
arrays:	upruns[20](0), downruns[20](0);

// up, down, and unchanged days, wiht averages
if close > close[1] then begin
		updays = updays + 1;
		avgup = avgup + close - close[1];
		end
	else if close < close[1] then begin
		downdays = downdays + 1;
		avgdown = avgdown + close - close[1];
		end
	else unchdays = unchdays + 1;

// runs
	if currentrun = 0 then begin
			if close > close[1] then currentrun = currentrun + 1
				else if close < close[1] then currentrun = currentrun - 1;
			end
		else if currentrun > 0 then begin
			if close > close[1] then begin
					currentrun = currentrun + 1;
					end
				else if close < close[1] then begin
					if currentrun > 20 then currentrun = 20;
					if currentrun > 0 then upruns[currentrun] = upruns[currentrun] + 1;
					currentrun = -1;
					end;
			end
		else if currentrun < 0 then begin
			if close < close[1] then begin
					currentrun = currentrun - 1;
					end
				else if close > close[1] then begin
					kx = -currentrun;
					if kx > 20 then kx = 20;
					if kx > 0 then downruns[kx] = downruns[kx] + 1;
					currentrun = 1;
				end;
		end;

// print
	if lastbaronchart then begin
		print(file("c:\tradestation\Up-Down and Runs.csv"),"Up days,Down days,Avg Up,Avg Down");
		print(file("c:\tradestation\Up-Down and Runs.csv"),updays:5:0,",",downdays:5:0,",",
						avgup/updays:6:2,",",avgdown/downdays:6:2);
		print(file("c:\tradestation\Up-Down and Runs.csv"),"Sequence of runs");
		print(file("c:\tradestation\Up-Down and Runs.csv"),",1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20");
		print(file("c:\tradestation\Up-Down and Runs.csv"),"RunUp,",upruns[1]:3:0,",",upruns[2]:3:0,",",upruns[3]:3:0,",",
				upruns[4]:3:0,",",upruns[5]:3:0,",",upruns[6]:3:0,",",upruns[7]:3:0,",",upruns[8]:3:0,",",
				upruns[9]:3:0,",",upruns[10]:3:0,",",upruns[11]:3:0,",",upruns[12]:3:0,",",upruns[13]:3:0,",",
				upruns[14]:3:0,",",upruns[15]:3:0,",",upruns[16]:3:0,",",upruns[17]:3:0,",",upruns[18]:3:0,",",
				upruns[19]:3:0,",",upruns[20]:3:0,",");

		print(file("c:\tradestation\Up-Down and Runs.csv"),"RunDown,",downruns[1]:3:0,",",downruns[2]:3:0,",",downruns[3]:3:0,",",
				downruns[4]:3:0,",",downruns[5]:3:0,",",downruns[6]:3:0,",",downruns[7]:3:0,",",downruns[8]:3:0,",",
				downruns[9]:3:0,",",downruns[10]:3:0,",",downruns[11]:3:0,",",downruns[12]:3:0,",",downruns[13]:3:0,",",
				downruns[14]:3:0,",",downruns[15]:3:0,",",downruns[16]:3:0,",",downruns[17]:3:0,",",downruns[18]:3:0,",",
				downruns[19]:3:0,",",downruns[20]:3:0,",");
		end;