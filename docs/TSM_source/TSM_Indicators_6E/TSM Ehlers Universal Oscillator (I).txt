//	 Universal Oscillator
// From John Ehlers, "Whiter is Brighter," Technical Analysis of Stocks & Commodities (January 2015)
//	(c) 2014  John F. Ehlers

Inputs:
	BandEdge(20);
	
Vars:
	WhiteNoise(0),
	a1(0),
	b1(0),
	c1(0),
	c2(0),
	c3(0),
	Filt(0),
	Peak(0),
	Universal(0);
	
WhiteNoise = (Close - Close[2]) / 2;	

// SuperSmoother Filter
a1 = expvalue(-1.414*3.14159 / BandEdge);
b1 = 2*a1*Cosine(1.414*180 / BandEdge);
c2 = b1;
c3 = -a1*a1;
c1 = 1 - c2 - c3;
Filt = c1*( WhiteNoise + WhiteNoise [1]) / 2 + c2*Filt[1] + c3*Filt[2];
{If Currentbar = 1 Then Filt = 0;
If Currentbar = 2 Then Filt = c1*0*(Close + Close[1])/2 + c2*Filt[1];
If Currentbar = 3 Then Filt = c1*0*(Close + Close[1])/2 + c2*Filt[1] + c3*Filt[2]; }

// Automatic Gain Control (AGC)
Peak = .991*Peak[1];
If Currentbar = 1 Then Peak = .0000001;
If AbsValue(Filt) > Peak Then Peak = AbsValue(Filt);
If Peak <> 0 Then Universal = Filt / Peak;

Plot1(Universal);
Plot2(0);
